module FPath.AbsDir
  ( AbsDir, AbsDirAs( _AbsDir_ ), AsAbsDir( _AbsDir )
  , AsNonRootAbsDir( _NonRootAbsDir ), NonRootAbsDir

  , absdirT
  -- quasi-quoters
  , absdir, absdirN

  , nonRootAbsDir
  , root

  , parseAbsDirP , parseAbsDirP'  , __parseAbsDirP__

  , tests
  )
where

import Base1T  hiding  ( toList )
import Prelude  ( error )

-- base --------------------------------

import qualified  Data.List.NonEmpty  as  NonEmpty

import Data.Foldable        ( concat, foldMap )
import Data.Monoid          ( Monoid )
import Data.Ord             ( Ordering( GT ), comparing )
import Data.Typeable        ( Proxy( Proxy ), TypeRep, typeRep )
import GHC.Exts             ( IsList( toList ) )
import GHC.Generics         ( Generic )

-- base-unicode-symbols ----------------

import Data.Monoid.Unicode    ( (‚àÖ) )

-- containers --------------------------

import qualified  Data.Sequence  as  Seq

-- data-textual ------------------------

import Data.Textual  ( Parsed( Parsed ), Textual( textual )
                     , fromString, parseString )

-- deepseq -----------------------------

import Control.DeepSeq  ( NFData )

-- lens --------------------------------

import Control.Lens.Cons    ( unsnoc )
import Control.Lens.Getter  ( view )
import Control.Lens.Iso     ( iso )

-- monaderror-io -----------------------

import MonadError  ( —≠ )

-- mono-traversable --------------------

import Data.MonoTraversable  ( Element
                             , MonoFoldable( oall, oany, ocompareLength, oelem
                                           , ofoldl', ofoldl1Ex', ofoldlM
                                           , ofoldMap, ofoldMap1Ex, ofoldr
                                           , ofoldr1Ex, olength, olength64
                                           , onotElem, onull, otoList
                                           )
                             , MonoFunctor( omap )
                             , maximumByEx, minimumByEx, unsafeHead, unsafeLast
                             )
import Data.NonNull          ( fromNullable )

-- more-unicode ------------------------

import Data.MoreUnicode.Function         ( (‚Öã) )
import Data.MoreUnicode.Lens             ( (‚ä©), (‚´£) )
import Data.MoreUnicode.MonoTraversable  ( (‚™¶), (‚™ß) )

-- non-empty-containers ----------------

import qualified  NonEmptyContainers.SeqConversions  as  SeqConversions
import qualified  NonEmptyContainers.SeqNE           as  SeqNE

import NonEmptyContainers.IsNonEmpty  ( FromMonoNonEmpty( fromNonEmpty )
                                      , IsMonoNonEmpty( nonEmpty )
                                      , ToMonoNonEmpty( toNonEmpty )
                                      , defaultNonEmpty
                                      )
import NonEmptyContainers.SeqConversions
                                    ( FromSeq( fromSeq ), IsSeq( seq )
                                    , ToSeq( toSeq ) )
import NonEmptyContainers.SeqNE     ( SeqNE( (:‚´∏) ), (‚´∏), (‚™™) )
import NonEmptyContainers.SeqNEConversions
                                    ( FromSeqNonEmpty( fromSeqNE )
                                    , IsSeqNonEmpty( seqNE )
                                    , ToSeqNonEmpty( toSeqNE, toSeq_ ) )

-- parsers -----------------------------

import Text.Parser.Char         ( char )
import Text.Parser.Combinators  ( endBy, endByNonEmpty )

-- quasiquoting ------------------------

import QuasiQuoting  ( QuasiQuoter, mkQQ, exp )

-- QuickCheck --------------------------

import Test.QuickCheck.Arbitrary  ( Arbitrary( arbitrary, shrink ) )

-- tasty-plus --------------------------

import TastyPlus  ( (‚â£), (‚âü), assertListEq, propInvertibleString
                  , propInvertibleText, propInvertibleUtf8 )

-- tasty-quickcheck --------------------

import Test.Tasty.QuickCheck  ( testProperty )

-- template-haskell --------------------

import Language.Haskell.TH         ( ExpQ )
import Language.Haskell.TH.Syntax  ( Lift )

-- text --------------------------------

import qualified  Data.Text  as  Text
import Data.Text  ( Text, any, length, empty, splitOn )

-- text-printer ------------------------

import qualified  Text.Printer  as  P

------------------------------------------------------------
--                     local imports                      --
------------------------------------------------------------

import FPath.AsFilePath        ( AsFilePath( filepath ) )
import FPath.AsFilePath'       ( AsFilePath'( filepath' )
                               , exterminate, terminate )
import FPath.Basename          ( Basename( basename, updateBasename ) )
import FPath.Dirname           ( Ancestors( ancestors )
                               , HasDirname( ancestors', dirname ) )
import FPath.DirType           ( DirTypeC( DirType ) )
import FPath.Error.FPathComponentError
                               ( FPathComponentError
                               , fPathComponentEmptyE
                               , fPathComponentIllegalCharE
                               )
import FPath.Error.FPathError  ( AsFPathError, FPathError( FPathComponentE )
                               , __FPathComponentE__
                               , __FPathEmptyE__
                               , __FPathNonAbsE__
                               , __FPathNotADirE__
                               , __FPathRootDirE__
                               , fPathEmptyE, fPathNonAbsE, fPathNotADirE
                               , fPathRootDirE
                               , mapTypeRepE
                               )
import FPath.Parent            ( HasParent( parent )
                               , HasParentMay( parentMay, parents ) )
import FPath.Parseable         ( Parseable( parse, parse' ) )
import FPath.PathComponent     ( PathComponent, parsePathC, pc, stub, toUpper )
import FPath.RelDir            ( RelDir, reldir )
import FPath.RelType           ( RelTypeC( RelType ) )
import FPath.Util              ( __ERROR'__ )

-------------------------------------------------------------------------------

{- | A non-root absolute directory, e.g., /etc. -}
-- a non-root dir is a path component appended to a (possibly-root) absolute
-- directory
newtype NonRootAbsDir = NonRootAbsDir (SeqNE PathComponent)
  deriving (Eq,Generic,NFData,Lift)

type instance Element NonRootAbsDir = PathComponent

instance Show NonRootAbsDir where
  show r = [fmt|[absdir|%T%s]|] (toText r) "|"

showTests ‚à∑ TestTree
showTests =
  testGroup "show"
            [ testCase "root"  $ "[absdir|/|]"           ‚âü show root
            , testCase "etc"   $ "[absdir|/etc/|]"       ‚âü show etc
            , testCase "pam.d" $ "[absdir|/etc/pam.d/|]" ‚âü show pamd
            ]

--------------------

instance Ord NonRootAbsDir where
  a <= b = toText a ‚â§ toText b

--------------------

instance DirTypeC AbsDir where
  type DirType AbsDir = AbsDir

--------------------

instance RelTypeC AbsDir where
  type RelType AbsDir = RelDir

--------------------

instance RelTypeC NonRootAbsDir where
  type RelType NonRootAbsDir = RelDir

--------------------

{- | An absolute directory is either the root directory, or a non-root absolute
     directory -}
data AbsDir = AbsRootDir | AbsNonRootDir NonRootAbsDir
  deriving (Eq,Generic,NFData,Lift)

absNonRootDir ‚à∑ SeqNE PathComponent ‚Üí AbsDir
absNonRootDir = AbsNonRootDir ‚àò NonRootAbsDir

type instance Element AbsDir = PathComponent

instance Show AbsDir where
  show r = [fmt|[absdir|%T%s]|] (toText r) "|"

------------------------------------------------------------

instance Ord AbsDir where
  a <= b = toText a ‚â§ toText b

--------------------

class AsNonRootAbsDir Œ± where
  _NonRootAbsDir ‚à∑ Prism' Œ± NonRootAbsDir

instance AsNonRootAbsDir NonRootAbsDir where
  _NonRootAbsDir = id

instance AsNonRootAbsDir AbsDir where
  _NonRootAbsDir =
    prism' AbsNonRootDir (\ case (AbsNonRootDir n) ‚Üí Just n
                                 AbsRootDir        ‚Üí Nothing)
--------------------

instance DirTypeC NonRootAbsDir where
  type DirType NonRootAbsDir = AbsDir

--------------------

{-| Things that may convert to an `AbsDir` (but an `AbsDir` will always convert
    to); e.g., @Dir@, @Abs@, @FPath@. -}
class AsAbsDir Œ± where
  _AbsDir ‚à∑ Prism' Œ± AbsDir

instance AsAbsDir AbsDir where
  _AbsDir = id

--------------------

{-| Things that /may/ be converted from an `AbsDir` (but will always convert
    /to/ an `AbsDir`). -}
class AbsDirAs Œ± where
  _AbsDir_ ‚à∑ Prism' AbsDir Œ±

instance AbsDirAs AbsDir where
  _AbsDir_ = id

instance AbsDirAs NonRootAbsDir where
  _AbsDir_ =
    prism' AbsNonRootDir (\ case AbsRootDir ‚Üí ùïπ; AbsNonRootDir d ‚Üí ùïµ d)

------------------------------------------------------------

instance MonoFunctor NonRootAbsDir where
  omap ‚à∑ (PathComponent ‚Üí PathComponent) ‚Üí NonRootAbsDir ‚Üí NonRootAbsDir
  omap f (NonRootAbsDir ps) = NonRootAbsDir (omap f ps)

instance MonoFunctor AbsDir where
  omap ‚à∑ (PathComponent ‚Üí PathComponent) ‚Üí AbsDir ‚Üí AbsDir
  omap _ AbsRootDir = AbsRootDir
  omap f (AbsNonRootDir d) = AbsNonRootDir (omap f d)

----------

monoFunctorTests ‚à∑ TestTree
monoFunctorTests =
  testGroup "MonoFunctor"
            [ testCase "usr" $
                    etc ‚âü omap (const [pc|etc|]) etc
            , testCase "wgm.d" $
                  fromSeqNE @AbsDir([pc|w.d|] ‚™™ [pc|g.d|] ‚™™ pure [pc|M.d|])
                ‚âü (‚óá [pc|.d|]) ‚™ß wgm
            , testCase "WGM" $
                  fromSeqNE @AbsDir([pc|W|] ‚™™ [pc|G|] ‚™™ pure [pc|M|])
                ‚âü wgm ‚™¶ toUpper
            ]

----------------------------------------

nonRootAbsDir ‚à∑ Prism' AbsDir NonRootAbsDir
nonRootAbsDir = prism' AbsNonRootDir go
                where go AbsRootDir        = Nothing
                      go (AbsNonRootDir d) = Just d

----------------------------------------

instance MonoFoldable AbsDir where
  otoList ‚à∑ AbsDir ‚Üí [PathComponent]
  otoList = toList
  ofoldl' ‚à∑ (Œ± ‚Üí PathComponent ‚Üí Œ±) ‚Üí Œ± ‚Üí AbsDir ‚Üí Œ±
  ofoldl' f x r = foldl' f x (toList r)

  ofoldr ‚à∑ (PathComponent ‚Üí Œ± ‚Üí Œ±) ‚Üí Œ± ‚Üí AbsDir ‚Üí Œ±
  ofoldr f x r = foldr f x (toList r)
  ofoldMap ‚à∑ Monoid ŒΩ => (PathComponent ‚Üí ŒΩ) ‚Üí AbsDir ‚Üí ŒΩ
  ofoldMap f r = foldMap f (toList r)
  ofoldr1Ex ‚à∑ (PathComponent ‚Üí PathComponent ‚Üí PathComponent) ‚Üí AbsDir
            ‚Üí PathComponent
  ofoldr1Ex f r = foldr1 f (toList r)
  ofoldl1Ex' ‚à∑ (PathComponent ‚Üí PathComponent ‚Üí PathComponent) ‚Üí AbsDir
             ‚Üí PathComponent
  ofoldl1Ex' f r = foldl1 f (toList r)

--------------------

instance MonoFoldable NonRootAbsDir where
  otoList ‚à∑ NonRootAbsDir ‚Üí [PathComponent]
  otoList = NonEmpty.toList ‚àò toNonEmpty
  ofoldl' ‚à∑ (Œ± ‚Üí PathComponent ‚Üí Œ±) ‚Üí Œ± ‚Üí NonRootAbsDir ‚Üí Œ±
  ofoldl' f x r = foldl' f x (toNonEmpty r)

  ofoldr ‚à∑ (PathComponent ‚Üí Œ± ‚Üí Œ±) ‚Üí Œ± ‚Üí NonRootAbsDir ‚Üí Œ±
  ofoldr f x r = foldr f x (toNonEmpty r)
  ofoldMap ‚à∑ Monoid ŒΩ => (PathComponent ‚Üí ŒΩ) ‚Üí NonRootAbsDir ‚Üí ŒΩ
  ofoldMap f r = foldMap f (toNonEmpty r)
  ofoldr1Ex ‚à∑ (PathComponent ‚Üí PathComponent ‚Üí PathComponent) ‚Üí NonRootAbsDir
            ‚Üí PathComponent
  ofoldr1Ex f r = foldr1 f (toNonEmpty r)
  ofoldl1Ex' ‚à∑ (PathComponent ‚Üí PathComponent ‚Üí PathComponent) ‚Üí NonRootAbsDir
             ‚Üí PathComponent
  ofoldl1Ex' f r = foldl1 f (toNonEmpty r)

newtype B = B Bool
 deriving Eq

instance Printable B where
  print (B b) = P.string (show b)

monoFoldableTests ‚à∑ TestTree
monoFoldableTests =
  testGroup "MonoFoldable"
            [ testCase "ofoldMap" $
                "w-g-M-" ‚âü ofoldMap ((‚äï "-") ‚àò toText) wgm
            , testCase "ofoldr" $
                "w-g-M-—Ñ" ‚âü ofoldr (\ a b ‚Üí toText a ‚äï "-" ‚äï b) "—Ñ" wgm
            , testCase "ofoldl'" $
                "—Ñ-w-g-M" ‚âü ofoldl' (\ b a ‚Üí b ‚äï "-" ‚äï toText a) "—Ñ" wgm
            , testCase "otoList" $
                [ [pc|w|], [pc|g|], [pc|M|] ] @=? otoList wgm
            , testCase "oall (F)" $
                ùï± @=? oall (any (‚â° 'r' ) ‚àò toText) wgm
            , testCase "oall (T)" $
                ùïø @=? oall ((< 6) ‚àò length ‚àò toText) wgm
            , testCase "oany (F)" $
                ùï± @=? oany (any (‚â° 'x' ) ‚àò toText) wgm
            , testProperty "onull" (\ x ‚Üí B (x ‚â° root) ‚â£ B (onull x))
            , testCase "olength" $
                3 ‚âü olength wgm
            , testCase "olength64" $
                0 ‚âü olength64 root
            , testCase "ocompareLength" $
               GT @=? ocompareLength wgm (2 ‚à∑ ‚Ñï)
            , testCase "ofoldlM" $
                    Just [[pc|M|],[pc|g|],[pc|w|]]
                @=? ofoldlM (\ a e ‚Üí Just $ e : a) [] wgm
            , testCase "ofoldMap1Ex" $
                [[pc|w|],[pc|g|],[pc|M|]] @=? ofoldMap1Ex pure wgm
            , testCase "ofoldr1Ex" $
                [pc|wgM|] ‚âü ofoldr1Ex (‚óá) wgm
            , testCase "ofoldl1Ex'" $
                [pc|wgM|] ‚âü ofoldl1Ex' (‚óá) wgm
            , testCase "unsafeHead" $
                [pc|w|] ‚âü unsafeHead wgm
            , testCase "unsafeLast" $
                [pc|M|] ‚âü unsafeLast wgm
            , testCase "maximumByEx" $
                [pc|w|] ‚âü maximumByEx (comparing toText) wgm
            , testCase "minimumByEx" $
                [pc|M|] ‚âü minimumByEx (comparing toText) wgm
            , testCase "oelem (T)" $
                ùïø @=? oelem [pc|g|] wgm
            , testCase "oelem (F)" $
                ùï± @=? oelem [pc|x|] wgm
            , testCase "onotElem (T)" $
                ùïø @=? onotElem [pc|x|] wgm
            , testCase "onotElem (F)" $
                ùï± @=? onotElem [pc|g|] wgm
            ]

----------------------------------------

instance FromSeqNonEmpty NonRootAbsDir where
  fromSeqNE ‚à∑ SeqNE PathComponent ‚Üí NonRootAbsDir
  fromSeqNE = NonRootAbsDir

instance FromSeqNonEmpty AbsDir where
  fromSeqNE = AbsNonRootDir ‚àò fromSeqNE

----------------------------------------

instance ToSeqNonEmpty NonRootAbsDir where
  toSeqNE (NonRootAbsDir ps) = ps

----------------------------------------

instance IsSeqNonEmpty NonRootAbsDir where
  seqNE = iso toSeqNE fromSeqNE

----------------------------------------

instance FromSeq AbsDir where
  fromSeq xs = case fromNullable xs of
                 Nothing ‚Üí AbsRootDir
                 Just ps ‚Üí fromSeqNE (toSeqNE ps)

----------------------------------------

instance ToSeq AbsDir where
  toSeq AbsRootDir = (‚àÖ)
  toSeq (AbsNonRootDir n) = toSeq n

instance ToSeq NonRootAbsDir where
  toSeq = toSeq_

----------------------------------------

instance IsSeq AbsDir where
  seq = iso toSeq fromSeq

isMonoSeqGetterTests ‚à∑ TestTree
isMonoSeqGetterTests =
  testGroup "getter"
            [ testCase "root"  $ Seq.Empty               @=? root ‚ä£ seq
            , testCase "etc"   $ pure [pc|etc|]          @=? etc  ‚ä£ seq
            , testCase "pam.d" $ [[pc|etc|],[pc|pam.d|]] @=? toList (pamd ‚ä£ seq)
            , testCase "wgm" $ [[pc|w|],[pc|g|],[pc|M|]] @=? toList (wgm  ‚ä£ seq)
            ]

isMonoSeqSetterTests ‚à∑ TestTree
isMonoSeqSetterTests =
  let x ~~ y = x & seq ‚ä¢ Seq.fromList y
   in testGroup "setter"
                [ testCase "etc"   $ etc   ‚âü root ~~ [ [pc|etc|] ]
                , testCase "root"  $ root  ‚âü etc ~~ []
                , testCase "d.pam" $
                      dpam ‚âü dpam ~~ [ [pc|pam.d|], [pc|etc|] ]
                , testCase "wgm"   $
                      wgm   ‚âü (‚´£ seq) (Seq.fromList [[pc|w|],[pc|g|],[pc|M|]])
                ]

isMonoSeqTests ‚à∑ TestTree
isMonoSeqTests =
  testGroup "IsMonoSeq" [ isMonoSeqGetterTests, isMonoSeqSetterTests ]

----------------------------------------

instance IsList AbsDir where
  type instance Item AbsDir = PathComponent
  fromList = fromSeq ‚àò Seq.fromList
  toList   = toList ‚àò toSeq

isListTests ‚à∑ TestTree
isListTests =
  testGroup "IsList"
    [ testGroup "fromList"
                [ testCase "root"  $ root ‚âü fromList []
                , testCase "etc"   $ etc  ‚âü fromList [ [pc|etc|] ]
                , testCase "pam.d" $ pamd ‚âü fromList [ [pc|etc|], [pc|pam.d|] ]
                , testCase "wgm"   $ wgm  ‚âü fromList [ [pc|w|],[pc|g|],[pc|M|] ]
                ]
    , testGroup "toList"
                [ testCase "root"  $
                      []                            @=? toList root
                , testCase "etc"   $
                      [ [pc|etc|] ]                 @=? toList etc
                , testCase "pam.d" $
                      [ [pc|etc|], [pc|pam.d|] ]    @=? toList pamd
                , testCase "wgm"   $
                      [ [pc|w|], [pc|g|], [pc|M|] ] @=? toList wgm
                ]
    ]

----------------------------------------

instance FromMonoNonEmpty NonRootAbsDir where
  fromNonEmpty = fromSeqNE ‚àò fromNonEmpty

----------------------------------------

instance ToMonoNonEmpty NonRootAbsDir where
  toNonEmpty   = toNonEmpty ‚àò toSeqNE

----------------------------------------

instance IsMonoNonEmpty NonRootAbsDir where
  nonEmpty = defaultNonEmpty

----------------------------------------

{- | Convert a sequence of printable elements to strings by intercalating '/'
     characters; with a post-fact string transformation for extra bells (e.g.,
     a prefix '/' character -}
pDir ‚à∑ (P.Printer œÅ, ToSeq Œ±, Printable (Element Œ±)) ‚áí
       (String ‚Üí String) ‚Üí Œ± ‚Üí œÅ
pDir f =  P.string ‚àò f ‚àò concat ‚àò fmap ((‚äï "/") ‚àò toString) ‚àò toSeq

instance Printable AbsDir where
  print = pDir ("/" ‚äï)

instance Printable NonRootAbsDir where
  print = pDir ("/" ‚äï)

printableTests ‚à∑ TestTree
printableTests =
  testGroup "printable"
            [ testCase "root"  $ "/"           ‚âü toText root
            , testCase "etc"   $ "/etc/"       ‚âü toText etc
            , testCase "pam.d" $ "/etc/pam.d/" ‚âü toText pamd
            , testCase "wgm"   $ "/w/g/M/"     ‚âü toText wgm
            ]

----------------------------------------

instance AsFilePath AbsDir where
  filepath = prism' toString fromString

instance AsFilePath NonRootAbsDir where
  filepath = prism' toString fromString

instance AsFilePath' AbsDir where
  filepath' = prism' (exterminate ‚àò toString)
                     (fromString ‚àò terminate)

instance AsFilePath' NonRootAbsDir where
  filepath' = prism' (exterminate ‚àò toString)
                     (fromString ‚àò terminate)

filepathTests ‚à∑ TestTree
filepathTests =
  let nothin' = Nothing ‚à∑ Maybe AbsDir
      fail s  = testCase s $ nothin' @=? s ‚©º filepath
   in testGroup "filepath"
            [ testCase "root"  $ "/"           ‚âü root    ‚´• filepath
            , testCase "etc"   $ "/etc/"       ‚âü etc     ‚´• filepath
            , testCase "pam.d" $ "/etc/pam.d/" ‚âü pamd    ‚´• filepath
            , testCase "wgm"   $ "/w/g/M/"     ‚âü wgm     ‚´• filepath
            , testCase "/etc/" $ Just etc      @=? "/etc/" ‚©º filepath
            , fail "/etc"
            , fail "/etc/pam.d"
            , fail "etc/"
            , fail "etc/pam.d"
            , fail "/etc//pam.d/"
            , fail "e/c"
            , fail "\0etc"
            , fail "etc\0"
            , fail "e\0c"
            ]

----------------------------------------

instance Textual AbsDir where
  textual = SeqConversions.fromList ‚ä≥ (char '/' ‚ã´ endBy textual (char '/'))

instance Textual NonRootAbsDir where
  textual =
    fromSeqNE ‚àò fromNonEmpty ‚ä≥ (char '/' ‚ã´ endByNonEmpty textual (char '/'))

textualTests ‚à∑ TestTree
textualTests =
  let nothin'     ‚à∑ Maybe AbsDir
      nothin'     = Nothing
      success e s = testCase s $ Parsed e  @=? parseString s
      fail s      = testCase s $ nothin'   @=? fromString s
   in testGroup "Textual"
                [ success root "/"
                , success etc  "/etc/"
                , success pamd "/etc/pam.d/"
                , fail "/etc"
                , fail "/etc/pam.d"
                , fail "etc/"
                , fail "etc/pam.d"
                , fail "/etc//pam.d/"
                , fail "e/c"
                , fail "\0etc"
                , fail "etc\0"
                , fail "e\0c"
                , testProperty "parseString - toString"
                               (propInvertibleString @AbsDir)
                , testProperty "parseText - toText" (propInvertibleText @AbsDir)
                , testProperty "parseUtf8 - toUtf8" (propInvertibleUtf8 @AbsDir)
                ]

----------------------------------------

instance Arbitrary AbsDir where
  arbitrary = fromSeq ‚ä≥ arbitrary
  shrink = fromSeq ‚©∫ shrink ‚àò toSeq

instance Arbitrary NonRootAbsDir where
  arbitrary = fromSeqNE ‚ä≥ arbitrary
  shrink = fromSeqNE ‚©∫ shrink ‚àò toSeqNE

----------------------------------------

instance HasParent NonRootAbsDir where
  parent = lens getParent setParent
           where getParent ‚à∑ NonRootAbsDir ‚Üí AbsDir
                 getParent (SeqNE.unsnoc ‚àò toSeqNE ‚Üí (ps,_)) = fromSeq ps
                 setParent ‚à∑ NonRootAbsDir ‚Üí AbsDir ‚Üí NonRootAbsDir
                 setParent (SeqNE.unsnoc ‚àò toSeqNE ‚Üí (_, p)) d =
                   fromSeqNE (toSeq d ‚´∏ p)

----------

parentTests ‚à∑ TestTree
parentTests =
  let par d = (view parent) ‚ä≥ (d ‚©º nonRootAbsDir)
   in testGroup "parent"
                [ testCase "root"        $ Nothing   @=? par root
                , testCase "etc"         $ Just root @=? par etc
                , testCase "pamd"        $ Just etc  @=? par pamd
                ]

----------------------------------------

nonAbsRootGetParentMay ‚à∑ NonRootAbsDir ‚Üí Maybe AbsDir
nonAbsRootGetParentMay = Just ‚àò view parent


nonAbsRootSetParentMay  ‚à∑ NonRootAbsDir ‚Üí Maybe AbsDir ‚Üí NonRootAbsDir
nonAbsRootSetParentMay (SeqNE.unsnoc ‚àò toSeqNE ‚Üí (_,p)) d =
  fromSeqNE $ (maybe —Ñ toSeq d) ‚´∏ p

instance HasParentMay AbsDir where
  parentMay = lens getParentMay setParentMay
              where getParentMay ‚à∑ AbsDir ‚Üí Maybe AbsDir
                    getParentMay AbsRootDir                          = Nothing
                    getParentMay (AbsNonRootDir n) = nonAbsRootGetParentMay n

                    setParentMay ‚à∑ AbsDir ‚Üí Maybe AbsDir ‚Üí AbsDir
                    setParentMay AbsRootDir (Just d) = d
                    setParentMay AbsRootDir Nothing  = AbsRootDir
                    setParentMay (AbsNonRootDir n) d =
                      AbsNonRootDir $ nonAbsRootSetParentMay n d

--------------------

instance HasParentMay NonRootAbsDir where
  parentMay = lens nonAbsRootGetParentMay nonAbsRootSetParentMay

--------------------

parentMayTests ‚à∑ TestTree
parentMayTests =
  let -- (~~) ‚à∑ Œ± ‚Üí Œ± ‚Üí Œ±
      d ~~ d' = d & parentMay ‚ä© d'
   in testGroup "parentMay"
                [ testCase "root"   $ Nothing   @=? root  ‚ä£ parentMay
                , testCase "etc"    $ Just root @=? etc   ‚ä£ parentMay
                , testCase "pamd"  $ Just etc  @=? pamd ‚ä£ parentMay

                , testCase "etc ‚Üí root" $ etc @=? etc ~~ root
                , testCase "root ‚Üí etc" $ etc @=? root ~~ etc

                , testCase "pamd ‚Üí root" $
                    fromSeq @AbsDir (pure [pc|pam.d|]) @=? pamd ~~ root
                , testCase "root ‚Üí pamd" $ pamd @=? root ~~ pamd

                , testCase "etc ‚Üí wgm" $
                      fromSeqNE @AbsDir([pc|w|]‚™™[pc|g|]‚™™[pc|M|]‚™™pure[pc|etc|])
                    @=? etc ~~ wgm
                , testCase "wgm ‚Üí etc" $
                    fromSeqNE @AbsDir ([pc|etc|]‚™™pure [pc|M|]) @=? wgm ~~ etc

                , testCase "root ‚Üí wgm" $ wgm @=? root ~~ wgm
                , testCase "wgm ‚Üí root" $
                    fromSeq @AbsDir (pure [pc|M|]) @=? wgm ~~ root

                , testCase "pamd ‚Üí etc" $ pamd @=? pamd ~~ etc
                , testCase "etc ‚Üí pamd" $
                      fromSeqNE @AbsDir ([pc|etc|]‚™™[pc|pam.d|]‚™™pure [pc|etc|])
                    @=? etc ~~ pamd

                , testCase "pamd ‚Üí Nothing" $
                    fromSeq (pure [pc|pam.d|]) @=? (pamd & parentMay ‚ä¢ Nothing)
                ]

parentsTests ‚à∑ TestTree
parentsTests =
  let check t d ps = assertListEq t ps (parents d)
   in testGroup "parents" $ [ check "/"          root []
                            , check "/etc/"      etc  [root]
                            , check "/etc/pam.d" pamd [root,etc]
                            , check "/w/g/m/"    wgm  [root,w,wg]
                            , check "/etc/"      etcN  [root]
                            , check "/etc/pam.d" pamdN [root,etc]
                            , check "/w/g/m/"    wgmN  [root,w,wg]
                            ]

----------------------------------------

absdirT ‚à∑ TypeRep
absdirT = typeRep (Proxy ‚à∑ Proxy AbsDir)

nrabsdirT ‚à∑ TypeRep
nrabsdirT = typeRep (Proxy ‚à∑ Proxy NonRootAbsDir)

instance Parseable AbsDir where
  parse ‚à∑ (AsFPathError Œµ, MonadError Œµ Œ∑, Printable œÑ) ‚áí œÑ ‚Üí Œ∑ AbsDir
  parse (toText ‚Üí t) =
    let mkCompE ‚à∑ (AsFPathError Œµ', MonadError Œµ' Œ∑')‚áíFPathComponentError ‚Üí Œ∑' Œ±
        mkCompE ce = __FPathComponentE__ ce absdirT t
        eCompE ‚à∑ (AsFPathError Œµ', MonadError Œµ' Œ∑') ‚áí
                 Either FPathComponentError Œ± ‚Üí Œ∑' Œ±
        eCompE = either mkCompE return
     in case unsnoc $ splitOn "/" t of
          Nothing            ‚Üí error "error: splitOn always returns something"
          Just (("":xs), "") ‚Üí case NonEmpty.nonEmpty xs of
                                 Nothing ‚Üí return $ root
                                 Just ys ‚Üí do
                                   ps ‚Üê eCompE $ mapM parsePathC ys
                                   return $ absNonRootDir (fromNonEmpty ps)

          Just ([],"")      ‚Üí __FPathEmptyE__ absdirT
          Just (("":_), _)  ‚Üí __FPathNotADirE__ absdirT t

          _                 ‚Üí __FPathNonAbsE__ absdirT t

--------------------

parseAbsDirTests ‚à∑ TestTree
parseAbsDirTests =
  let pamNUL      = "/etc/pam\0/"
      pamF        = "/etc/pam"
      illegalCE   = let fpcice = fPathComponentIllegalCharE '\0' "pam\0"
                     in FPathComponentE fpcice absdirT pamNUL
      emptyCompCE = FPathComponentE fPathComponentEmptyE absdirT "/etc//pam.d/"
      _parseAbsDir ‚à∑ MonadError FPathError Œ∑ ‚áí Text ‚Üí Œ∑ AbsDir
      _parseAbsDir = parse'
   in testGroup "parseAbsDir"
                [ testCase "root"  $ Right root   @=? _parseAbsDir "/"
                , testCase "etc"   $ Right etc    @=? _parseAbsDir "/etc/"
                , testCase "pam.d" $ Right pamd   @=? _parseAbsDir "/etc/pam.d/"
                , testCase "wgm"   $ Right wgm    @=? _parseAbsDir "/w/g/M/"
                , testCase "no trailing /" $
                      Left (fPathNotADirE absdirT pamF) @=? _parseAbsDir pamF
                , testCase "empty" $
                      Left (fPathEmptyE absdirT)  @=? _parseAbsDir ""
                , testCase "no leading /" $
                      Left (fPathNonAbsE absdirT "etc/") @=? _parseAbsDir "etc/"
                , testCase "bad component" $
                      Left illegalCE @=? _parseAbsDir pamNUL
                , testCase "empty component" $
                      Left emptyCompCE @=? _parseAbsDir "/etc//pam.d/"
                ]

----------------------------------------

{- | Like `parseAbsDir`, but non-empty input that doesn't end in a '/' character
     has a '/' appended rather than failing as a non-file (thus, "permissive
     `parseAbsDir`" -}
parseAbsDirP ‚à∑ ‚àÄ Œµ œÑ Œ∑ . (AsFPathError Œµ, MonadError Œµ Œ∑, Printable œÑ) ‚áí
               œÑ ‚Üí Œ∑ AbsDir
parseAbsDirP (toText ‚Üí t) =
  let safeLast "" = Nothing
      safeLast s  = Just $ Text.last s
   in case safeLast t of
        Nothing  ‚Üí parse empty
        Just '/' ‚Üí parse t
        _        ‚Üí parse (t ‚äï "/")

{-# DEPRECATED parseAbsDirP' "use parseAbsDirP @FPathError" #-}
parseAbsDirP' ‚à∑ (Printable œÑ, MonadError FPathError Œ∑) ‚áí œÑ ‚Üí Œ∑ AbsDir
parseAbsDirP' = parseAbsDirP

__parseAbsDirP__ ‚à∑ Printable œÑ ‚áí œÑ ‚Üí AbsDir
__parseAbsDirP__ = either __ERROR'__ id ‚àò parseAbsDirP'

__parseAbsDirP'__ ‚à∑ String ‚Üí AbsDir
__parseAbsDirP'__ = __parseAbsDirP__

--------------------

parseAbsDirPTests ‚à∑ TestTree
parseAbsDirPTests =
  let pamNUL      = "/etc/pam\0/"
      illegalCE   = let fpcice = fPathComponentIllegalCharE '\0' "pam\0"
                     in FPathComponentE fpcice absdirT pamNUL
      emptyCompCE = FPathComponentE fPathComponentEmptyE absdirT "/etc//pam.d/"
      _parseAbsDirP ‚à∑ MonadError FPathError Œ∑ ‚áí Text ‚Üí Œ∑ AbsDir
      _parseAbsDirP = parseAbsDirP'
   in testGroup "parseAbsDirP"
                [ testCase "root"  $ Right root   @=? _parseAbsDirP "/"
                , testCase "etc"   $ Right etc    @=? _parseAbsDirP "/etc/"
                , testCase "etc"   $ Right etc    @=? _parseAbsDirP "/etc"
                , testCase "pam.d" $ Right pamd   @=? _parseAbsDirP "/etc/pam.d"
                , testCase "pam.d" $
                      Right pamd   @=? _parseAbsDirP "/etc/pam.d/"
                , testCase "wgm"   $ Right wgm    @=? _parseAbsDirP "/w/g/M/"
                , testCase "wgm"   $ Right wgm    @=? _parseAbsDirP "/w/g/M"
                , testCase "empty" $
                      Left (fPathEmptyE absdirT)  @=? _parseAbsDirP ""
                , testCase "no leading /" $
                          Left (fPathNonAbsE absdirT "etc/")
                      @=? _parseAbsDirP "etc/"
                , testCase "bad component" $
                      Left illegalCE @=? _parseAbsDirP pamNUL
                , testCase "empty component" $
                      Left emptyCompCE @=? _parseAbsDirP "/etc//pam.d/"
                ]

----------------------------------------

instance Parseable NonRootAbsDir where
  parse ‚à∑ (AsFPathError Œµ, MonadError Œµ Œ∑, Printable œÑ) ‚áí œÑ ‚Üí Œ∑ NonRootAbsDir
  parse t = do mapTypeRepE (const nrabsdirT) $ parse t ‚â´ \ case
                 AbsRootDir ‚Üí __FPathRootDirE__ nrabsdirT
                 AbsNonRootDir d' ‚Üí return d'

--------------------

parseAbsDirNTests ‚à∑ TestTree
parseAbsDirNTests =
  let pamNUL      = "/etc/pam\0/"
      pamF        = "/etc/pam"
      illegalCE   = let fpcice = fPathComponentIllegalCharE '\0' "pam\0"
                     in FPathComponentE fpcice nrabsdirT pamNUL
      emptyCompCE = FPathComponentE fPathComponentEmptyE nrabsdirT "/etc//pam/"
      parseAbsDirN_ ‚à∑ MonadError FPathError Œ∑ ‚áí Text ‚Üí Œ∑ NonRootAbsDir
      parseAbsDirN_ = parse'
   in testGroup "parseAbsDirN"
                [ testCase "etc"   $ Right etcN    @=? parseAbsDirN_ "/etc/"
                , testCase "pam.d" $
                      Right pamdN   @=? parseAbsDirN_ "/etc/pam.d/"
                , testCase "wgm"   $ Right wgmN    @=? parseAbsDirN_ "/w/g/M/"
                , testCase "root"  $
                          Left (fPathRootDirE nrabsdirT)
                      @=? parseAbsDirN_ "/"
                , testCase "no trailing /" $
                          Left (fPathNotADirE nrabsdirT pamF)
                      @=? parseAbsDirN_ pamF
                , testCase "empty" $
                          Left (fPathEmptyE nrabsdirT)
                      @=? parseAbsDirN_ ""
                , testCase "no leading /" $
                         Left (fPathNonAbsE nrabsdirT "etc/")
                      @=? parseAbsDirN_ "etc/"
                , testCase "bad component" $
                      Left illegalCE @=? parseAbsDirN_ pamNUL
                , testCase "empty component" $
                      Left emptyCompCE @=? parseAbsDirN_ "/etc//pam/"
                ]

----------------------------------------

{- | Like `parseAbsDirN`, but non-empty input that doesn't end in a '/'
      character has a '/' appended rather than failing as a non-file (thus,
      "permissive `parseAbsDirN`" -}
parseAbsDirNP ‚à∑ (AsFPathError Œµ, MonadError Œµ Œ∑, Printable œÑ) ‚áí
               œÑ ‚Üí Œ∑ NonRootAbsDir
parseAbsDirNP (toText ‚Üí t) = do
  let safeLast "" = Nothing
      safeLast s  = Just $ Text.last s
   in case safeLast t of
        Nothing  ‚Üí parse empty
        Just '/' ‚Üí parse t
        _        ‚Üí parse (t ‚äï "/")

--------------------

parseAbsDirNP' ‚à∑ (Printable œÑ, MonadError FPathError Œ∑) ‚áí œÑ ‚Üí Œ∑ NonRootAbsDir
parseAbsDirNP' = parseAbsDirNP

--------------------

__parseAbsDirNP__ ‚à∑ HasCallStack ‚áí String ‚Üí NonRootAbsDir
__parseAbsDirNP__ s = case parse' s of
                       Left e ‚Üí __ERROR'__ e
                       Right AbsRootDir ‚Üí __ERROR'__ $ fPathRootDirE nrabsdirT
                       Right (AbsNonRootDir nr) ‚Üí nr

__parseAbsDirNP'__ ‚à∑ String ‚Üí NonRootAbsDir
__parseAbsDirNP'__ = __parseAbsDirNP__

--------------------

parseAbsDirNPTests ‚à∑ TestTree
parseAbsDirNPTests =
  let pamNUL      = "/etc/pam\0/"
      illegalCE   = let fpcice = fPathComponentIllegalCharE '\0' "pam\0"
                     in FPathComponentE fpcice nrabsdirT pamNUL
      emptyCompCE = FPathComponentE fPathComponentEmptyE nrabsdirT "/etc//pam/"
      parseAbsDirNP_ ‚à∑ MonadError FPathError Œ∑ ‚áí Text ‚Üí Œ∑ NonRootAbsDir
      parseAbsDirNP_ = parseAbsDirNP'
   in testGroup "parseAbsDirNP"
                [ testCase "etc"   $ Right etcN  @=? parseAbsDirNP_ "/etc"
                , testCase "pam.d" $ Right pamdN @=? parseAbsDirNP_ "/etc/pam.d"
                , testCase "wgm"   $ Right wgmN  @=? parseAbsDirNP_ "/w/g/M"
                , testCase "root"  $
                      Left (fPathRootDirE nrabsdirT) @=? parseAbsDirNP_ "/"
                , testCase "empty" $
                      Left (fPathEmptyE nrabsdirT)  @=? parseAbsDirNP_ ""
                , testCase "no leading /" $
                        Left (fPathNonAbsE nrabsdirT "etc/")
                      @=? parseAbsDirNP_ "etc/"
                , testCase "bad component" $
                      Left illegalCE @=? parseAbsDirNP_ pamNUL
                , testCase "empty component" $
                      Left emptyCompCE @=? parseAbsDirNP_ "/etc//pam/"
                ]

------------------------------------------------------------
--                     Quasi-Quoting                      --
------------------------------------------------------------

absdirQQ ‚à∑ String ‚Üí Maybe ExpQ
absdirQQ = (\ d ‚Üí ‚ü¶d‚üß) ‚©∫ (—≠ ‚àò parse @AbsDir @FPathError)

absdirNQQ ‚à∑ String ‚Üí Maybe ExpQ
absdirNQQ = (\ d ‚Üí ‚ü¶d‚üß) ‚©∫ (—≠ ‚àò parse @NonRootAbsDir @FPathError)

{- | quasi-quoter for AbsDir -}
absdir ‚à∑ QuasiQuoter
absdir = mkQQ "AbsDir" $ def & exp ‚ä© absdirQQ

{- | quasi-quoter for NonRootAbsDir -}

absdirN ‚à∑ QuasiQuoter
absdirN = mkQQ "NonRootAbsDir" $ def & exp ‚ä© absdirNQQ

----------------------------------------

instance HasDirname AbsDir where
  dirname ‚à∑ Lens' AbsDir AbsDir
  dirname = lens (\ case AbsRootDir ‚Üí AbsRootDir; AbsNonRootDir d ‚Üí d ‚ä£ dirname)
                 (\ o d ‚Üí case o of
                            AbsRootDir ‚Üí d
                            AbsNonRootDir n ‚Üí AbsNonRootDir $ n & parent ‚ä¢ d
                 )

  ancestors' ‚à∑ AbsDir ‚Üí [AbsDir]
  ancestors' fp | fp ‚â° root  = []
                | otherwise  = (fp ‚ä£ dirname) : ancestors' (fp ‚ä£ dirname)

absDirAncestors'Tests ‚à∑ TestTree
absDirAncestors'Tests =
  testGroup "AbsDir"
            [ testCase "root" $ []          @=? ancestors' root
            , testCase "etc"  $ [root]      @=? ancestors' etc
            , testCase "pamd" $ [etc,root]  @=? ancestors' pamd
            , testCase "wgm"  $ [wg,w,root] @=? ancestors' wgm
            ]

absDirDirnameTests ‚à∑ TestTree
absDirDirnameTests =
  testGroup "AbsDir"
            [ testCase "root"  $ root ‚âü root ‚ä£ dirname
            , testCase "pam.d" $ etc  ‚âü pamd ‚ä£ dirname

            , testCase "root  -> etc"     $ etc ‚âü root ‚Öã dirname ‚ä¢ etc
            , testCase "etc   -> etc"     $ etc ‚âü etc ‚Öã dirname ‚ä¢ root
            , testCase "pam.d -> pam.d"   $ pamd ‚âü pamd ‚Öã dirname ‚ä¢ etc
            , testCase "pam.d -> /pam.d"  $
                fromList [[pc|pam.d|]] ‚âü pamd ‚Öã dirname ‚ä¢ root
            , testCase "/pam.d -> pam.d"  $
                pamd ‚âü fromList [[pc|pam.d|]] ‚Öã dirname ‚ä¢ etc
            ]

--------------------

instance Ancestors NonRootAbsDir where
  ancestors ‚à∑ NonRootAbsDir ‚Üí NonEmpty AbsDir
  ancestors fp = (fp ‚ä£ dirname) :| ancestors' (fp ‚ä£ dirname)

nonRootAbsDirAncestorsTests ‚à∑ TestTree
nonRootAbsDirAncestorsTests =
  testGroup "NonRootAbsDir"
            [ testCase "etc"  $ pure root      @=? ancestors etcN
            , testCase "pamd" $ etc :| [root]  @=? ancestors pamdN
            , testCase "wgm"  $ wg :| [w,root] @=? ancestors wgmN
            ]

instance HasDirname NonRootAbsDir where
  dirname ‚à∑ Lens' NonRootAbsDir AbsDir
  dirname = parent

  ancestors' ‚à∑ NonRootAbsDir ‚Üí [AbsDir]
  ancestors' fp = (fp ‚ä£ dirname) : ancestors' (fp ‚ä£ dirname)

nonRootAbsDirDirnameTests ‚à∑ TestTree
nonRootAbsDirDirnameTests =
  testGroup "NonRootAbsDir"
            [ testCase "etcN"  $ root ‚âü etcN ‚ä£ dirname
            , testCase "wgmN"  $ fromList [[pc|w|],[pc|g|]] ‚âü wgmN ‚ä£ dirname

            , testCase "etc   -> etc"     $ etcN ‚âü etcN ‚Öã dirname ‚ä¢ root
            , testCase "pam.d -> pam.d"   $ pamdN ‚âü pamdN ‚Öã dirname ‚ä¢ etc
            , testCase "pam.d -> /pam.d"  $
                fromNonEmpty (pure [pc|pam.d|]) ‚âü pamdN ‚Öã dirname ‚ä¢ root
            , testCase "/pam.d -> pam.d"  $
                pamdN ‚âü fromNonEmpty (pure [pc|pam.d|]) ‚Öã dirname ‚ä¢ etc
            ]

nonRootAbsDirAncestors'Tests ‚à∑ TestTree
nonRootAbsDirAncestors'Tests =
  testGroup "NonRootAbsDir"
            [ testCase "etc"  $ [root]      @=? ancestors' etcN
            , testCase "pamd" $ [etc,root]  @=? ancestors' pamdN
            , testCase "wgm"  $ [wg,w,root] @=? ancestors' wgmN
            ]

dirnameTests ‚à∑ TestTree
dirnameTests = testGroup "dirname" [ absDirDirnameTests
                                   , nonRootAbsDirDirnameTests ]

ancestors'Tests ‚à∑ TestTree
ancestors'Tests = testGroup "ancestors'" [ absDirAncestors'Tests
                                         , nonRootAbsDirAncestors'Tests ]

----------------------------------------

instance Basename AbsDir where
  basename ‚à∑ AbsDir ‚Üí RelDir
  basename AbsRootDir = fromList []
  basename (AbsNonRootDir n) = basename n

  updateBasename ‚à∑ (PathComponent ‚Üí PathComponent) ‚Üí AbsDir ‚Üí AbsDir
  updateBasename _ AbsRootDir = AbsRootDir
  updateBasename f (AbsNonRootDir n) = AbsNonRootDir (updateBasename f n)

absDirBasenameTests ‚à∑ TestTree
absDirBasenameTests =
  testGroup "AbsDir"
            [ testCase "root"  $ [reldir|./|] ‚âü basename root
            , testCase "pam.d" $ [reldir|pam.d/|]  ‚âü basename pamd

            , testCase "root  -> root"    $
                root ‚âü updateBasename (const [pc|etc|]) root
            , testCase "etc   -> etc"     $
                etc ‚âü updateBasename (const [pc|etc|]) etc
            , testCase "pam.d -> pam.d"   $
                pamd ‚âü updateBasename (const [pc|pam.d|]) pamd
            , testCase "pam.d -> pam.d"   $
                pamd ‚âü updateBasename (const [pc|pam.d|])
                                      (fromList [[pc|etc|], [pc|pam|]])
            ]

--------------------

instance Basename NonRootAbsDir where
  basename ‚à∑ NonRootAbsDir ‚Üí RelDir
  basename (NonRootAbsDir (_ :‚´∏ p)) = fromList [p]

  updateBasename ‚à∑ (PathComponent ‚Üí PathComponent) ‚Üí NonRootAbsDir
                 ‚Üí NonRootAbsDir
  updateBasename f (NonRootAbsDir (ps :‚´∏ p)) = NonRootAbsDir (ps :‚´∏ f p)

nonRootBasenameTests ‚à∑ TestTree
nonRootBasenameTests =
  let pam = fromNonEmpty $ [pc|etc|] :| [[pc|pam|]]
   in testGroup "NonRootAbsDir"
                [ testCase "etcN"  $ [reldir|etc/|] ‚âü basename etcN
                , testCase "wgmN"  $ [reldir|M/|]   ‚âü basename wgmN

                , testCase "etc   -> etc"   $
                    etcN  ‚âü updateBasename (const [pc|etc|]) etcN
                , testCase "pam.d -> pam" $ pam ‚âü updateBasename stub pamdN
                , testCase "/pam.d -> etc"  $
                    etcN ‚âü updateBasename
                             (\ p ‚Üí if p ‚â° [pc|pam.d|] then [pc|etc|] else p)
                             (fromNonEmpty $ pure [pc|pam.d|])
                ]

--------------------

basenameTests ‚à∑ TestTree
basenameTests =
  testGroup "basename" [ absDirBasenameTests, nonRootBasenameTests ]

----------------------------------------
--             constants              --
----------------------------------------

root ‚à∑ AbsDir
root = AbsRootDir

--------------------------------------------------------------------------------
--                                   tests                                    --
--------------------------------------------------------------------------------

-- test data ---------------------------

etc ‚à∑ AbsDir
etc = fromSeq $ pure [pc|etc|]

pamd ‚à∑ AbsDir
pamd = fromSeq $ [pc|etc|] ‚™™ pure [pc|pam.d|]

dpam ‚à∑ AbsDir
dpam = fromSeq $ [pc|pam.d|] ‚™™ pure [pc|etc|]

wgm ‚à∑ AbsDir
wgm = fromSeq $ [pc|w|] ‚™™ [pc|g|] ‚™™ pure [pc|M|]

wg ‚à∑ AbsDir
wg = fromSeq $ [pc|w|] ‚™™ pure [pc|g|]

w ‚à∑ AbsDir
w = fromSeq $ pure [pc|w|]

etcN ‚à∑ NonRootAbsDir
etcN = fromSeqNE $ pure [pc|etc|]

pamdN ‚à∑ NonRootAbsDir
pamdN = fromSeqNE $ [pc|etc|] ‚™™ pure [pc|pam.d|]

wgmN ‚à∑ NonRootAbsDir
wgmN = fromSeqNE $ [pc|w|] ‚™™ [pc|g|] ‚™™ pure [pc|M|]

----------------------------------------

constructionTests ‚à∑ TestTree
constructionTests = testGroup "construction" [ parseAbsDirTests
                                             , parseAbsDirNTests
                                             , parseAbsDirPTests
                                             , parseAbsDirNPTests
                                             ]

tests ‚à∑ TestTree
tests = testGroup "FPath.AbsDir" [ constructionTests, showTests
                                 , isListTests, filepathTests
                                 , dirnameTests, nonRootAbsDirAncestorsTests
                                 , ancestors'Tests, basenameTests
                                 , printableTests, textualTests
                                 , monoFoldableTests, isMonoSeqTests
                                 , monoFunctorTests
                                 , parentTests, parentMayTests, parentsTests
                                 ]

--------------------

_test ‚à∑ IO ExitCode
_test = runTestTree tests

--------------------

_tests ‚à∑ String ‚Üí IO ExitCode
_tests = runTestsP tests

_testr ‚à∑ String ‚Üí ‚Ñï ‚Üí IO ExitCode
_testr = runTestsReplay tests

-- that's all, folks! ----------------------------------------------------------
